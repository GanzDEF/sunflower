/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    // Define versions in a single place
    ext {
        // Sdk and tools
        compileSdkVersion = 28
        minSdkVersion = 21
        targetSdkVersion = 28
        composeSnapshot = '6602655' // dev14

        // App dependencies
        accompanistVersion = "0.1.6.ui-$composeSnapshot-SNAPSHOT"
        appCompatVersion = '1.1.0'
        composeKotlinCompilerVersion = '1.3.70-dev-withExperimentalGoogleExtensions-20200424'
        composeVersion = '0.1.0-SNAPSHOT'
        constraintLayoutVersion = '2.0.0-beta3'
        coreTestingVersion = '2.1.0'
        coroutinesVersion = "1.3.0-M2"
        espressoVersion = '3.2.0'
        fragmentVersion = '1.1.0-alpha09'
        glideVersion = '4.10.0'
        gradleVersion = '4.2.0-alpha03'
        gsonVersion = '2.8.2'
        junitVersion = '4.13'
        kotlinVersion = '1.3.72'
        ktlintVersion = '0.37.0'
        ktxVersion = '1.0.2'
        lifecycleVersion = '2.2.0-alpha01'
        materialVersion = '1.1.0-alpha09'
        navigationVersion = '2.2.0'
        recyclerViewVersion = '1.1.0-alpha05'
        roomVersion = '2.1.0'
        runnerVersion = '1.0.1'
        truthVersion = '0.42'
        testExtJunit = '1.1.0'
        uiAutomatorVersion = '2.2.0'
        viewPagerVersion = '1.0.0'
        workVersion = '2.1.0'
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "4.3.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        // Compose snapshots repo
        maven { url "https://androidx.dev/snapshots/builds/$composeSnapshot/artifacts/ui/repository/" }
        // Accompanist snapshots repo
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        targetExclude("$buildDir/**/*.kt")
        targetExclude('bin/**/*.kt')
        ktlint(ktlintVersion)
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {

            // Don't require parens on fun type annotations e.g. `@Composable~()~ () -> Unit`
            // TODO: Remove when we move to Kotlin 1.4
            freeCompilerArgs += "-XXLanguage:+NonParenthesizedAnnotationsOnFunctionalTypes"

            // Compose is now based on the Kotlin 1.4 compiler, but we need to use the 1.3.x Kotlin
            // library due to library compatibility, etc. Therefore we explicit set our apiVersion
            // to 1.3 to fix any warnings. Binary dependencies (such as Compose) can continue to
            // use 1.4 if built with that library.
            // TODO: remove this once we move to Kotlin 1.4
            apiVersion = "1.3"

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}